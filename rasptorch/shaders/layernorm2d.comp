#version 450

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0, std430) readonly buffer BufIn { float x[]; } bufi;   // [N,C]
layout(set = 0, binding = 1, std430) writeonly buffer BufOut { float y[]; } bufo; // [N,C]

layout(push_constant) uniform Push {
    uint N;
    uint C;
} pc;

void main() {
    uint n = gl_GlobalInvocationID.x;
    if (n >= pc.N) return;

    uint base = n * pc.C;
    float mean = 0.0;
    for (uint c = 0u; c < pc.C; c++) {
        mean += bufi.x[base + c];
    }
    mean /= max(1.0, float(pc.C));

    float var = 0.0;
    for (uint c = 0u; c < pc.C; c++) {
        float d = bufi.x[base + c] - mean;
        var += d * d;
    }
    var /= max(1.0, float(pc.C));

    float invstd = inversesqrt(var + 1e-5);
    for (uint c = 0u; c < pc.C; c++) {
        bufo.y[base + c] = (bufi.x[base + c] - mean) * invstd;
    }
}
