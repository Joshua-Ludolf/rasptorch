#version 450

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0, std430) readonly buffer BufY { float logp[]; } bufy;   // [N,C] (log_softmax output)
layout(set = 0, binding = 1, std430) readonly buffer BufG { float go[]; } bufg;     // [N,C]
layout(set = 0, binding = 2, std430) writeonly buffer BufDX { float dx[]; } bufo;   // [N,C]

layout(push_constant) uniform Push {
    uint N;
    uint C;
} pc;

void main() {
    uint n = gl_GlobalInvocationID.x;
    if (n >= pc.N) return;

    uint base = n * pc.C;

    float sumg = 0.0;
    for (uint c = 0u; c < pc.C; c++) {
        sumg += bufg.go[base + c];
    }

    for (uint c = 0u; c < pc.C; c++) {
        float g = bufg.go[base + c];
        float s = exp(bufy.logp[base + c]);
        bufo.dx[base + c] = g - s * sumg;
    }
}
