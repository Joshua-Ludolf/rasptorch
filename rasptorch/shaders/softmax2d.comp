#version 450

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0, std430) readonly buffer BufIn { float x[]; } bufi;   // [N,C]
layout(set = 0, binding = 1, std430) writeonly buffer BufOut { float y[]; } bufo; // [N,C]

layout(push_constant) uniform Push {
    uint N;
    uint C;
} pc;

void main() {
    uint n = gl_GlobalInvocationID.x;
    if (n >= pc.N) return;

    uint base = n * pc.C;

    float m = -3.402823e38;
    for (uint c = 0u; c < pc.C; c++) {
        float v = bufi.x[base + c];
        m = max(m, v);
    }

    float sumexp = 0.0;
    for (uint c = 0u; c < pc.C; c++) {
        sumexp += exp(bufi.x[base + c] - m);
    }
    sumexp = max(sumexp, 1e-20);

    for (uint c = 0u; c < pc.C; c++) {
        bufo.y[base + c] = exp(bufi.x[base + c] - m) / sumexp;
    }
}
